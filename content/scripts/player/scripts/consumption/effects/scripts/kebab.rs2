[label,consume_effect_kebab]
mes("You eat the kebab.");
sound_synth(eat, 0, 0);
anim(human_eat, 0);
inv_delslot(inv, last_slot);
// wiki data from 17229 kebabs
// todo: figure out the order these go in. This is important because theres hp checks in some of these
def_int $random = random(32);
if ($random < 1) { // 1/32
    mes("Wow, that was an amazing kebab! You feel really invigorated.");
    stat_boost(attack, 2, 0);
    stat_boost(strength, 2, 0);
    stat_boost(defence, 2, 0);
    stat_heal(hitpoints, 7, 24);
} else if ($random < 9) { // 8/32
    mes("That was a good kebab. You feel a lot better.");
    stat_heal(hitpoints, 6, 14);
} else if ($random < 29) { // 20/32
    if (stat(hitpoints) < stat_base(hitpoints)) {
        mes("It heals some health.");
        stat_heal(hitpoints, 3, 7);
    }
} else if ($random < 30) { // 1/32
    mes("That tasted very dodgy. You feel very ill.");
    stat_drain(attack, 3, 0);
    stat_drain(strength, 3, 0);
    stat_drain(defence, 3, 0);
    if (stat(hitpoints) > 2) {
        ~damage_self(min(sub(stat(hitpoints), 2), 4));
    }
} else if ($random < 31 & stat(hitpoints) > 4) { // 1/32
    // in osrs you cant get this if the stat it picks is below 5
    mes("That tasted a bit dodgy. You feel a bit ill.");
    ~damage_self(3);
} else { // 1/32
    mes("The kebab didn't seem to do a lot.");
}
