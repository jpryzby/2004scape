[opnpc1,newbie_survival_instructor]
switch_int(%tutorial_progress) {
    case ^newbie_basics_instructor_interacted_with_door : @newbie_survival_instructor_start;
    case ^newbie_survival_instructor_open_inventory : @newbie_survival_instructor_look_at_menu;
    case ^newbie_survival_instructor_cut_tree, ^newbie_survival_instructor_build_fire : @newbie_survival_instructor_fire;
    case ^newbie_survival_instructor_open_skill_menu : @newbie_survival_instructor_look_at_menu;
    case ^newbie_survival_instructor_opened_skill_menu : @newbie_survival_instructor_fishing;
    case ^newbie_survival_instructor_fish_shrimps,
        ^newbie_survival_instructor_cook_shrimps,
        ^newbie_survival_instructor_burnt_shrimps : @newbie_survival_instructor_fishing_after_burning;
    case default : @newbie_survival_instructor_recap;
}

[label,newbie_survival_instructor_start]
~chatnpc("<p,neutral>Hello there newcomer. My name is Brynna. My job is|to teach you a few survival tips and tricks. First off|we're going to start with the most basic survival skill of|all; making a fire.");
~doubleobjbox(tinderbox, bronze_axe, "The Survival Guide gives you a |@blu@Tinderbox@bla@ |and a |@blu@Bronze Hatchet!", 200);
%tutorial_progress = ^newbie_survival_instructor_open_inventory;
~tutorial_step_view_inventory;

[label,newbie_survival_instructor_look_at_menu]
~chatnpc("<p,neutral>Hello again. You should take a look at that menu before we continue.");

[label,newbie_survival_instructor_fire]
~chatnpc("<p,neutral>Next up you need to make a fire. First, you'll need to cut down a tree to get some logs.");
~newbie_survival_instructor_replace_items;

// basing these obj boxes off this from 2005
// https://web.archive.org/web/20051215201825im_/http://runevillage.com/images/rsTutorial8.gif
// https://web.archive.org/web/20051215201825im_/http://runevillage.com/images/rsTutorial10.gif
// Looks like the exclamation points don't switch back to black.
[proc,newbie_survival_instructor_replace_items]
if (~tutorial_has_obj_on_person(bronze_axe) = false & inv_total(inv, tinderbox) < 1) {
    inv_add(inv, bronze_axe, 1);
    inv_add(inv, tinderbox, 1);
    ~doubleobjbox(tinderbox, bronze_axe, "The Survival Guide gives you a |@blu@Tinderbox@bla@ |and a |@blu@Bronze Hatchet!", 200);
} else if (~tutorial_has_obj_on_person(bronze_axe) = false) {
    inv_add(inv, bronze_axe, 1);
    ~objbox(bronze_axe, "The Survival Guide gives you a |@blu@Bronze Hatchet!", 250, 0, 0);
}else if (inv_total(inv, tinderbox) < 1) {
    inv_add(inv, tinderbox, 1);
    ~objbox(tinderbox, "The Survival Guide gives you a |@blu@Tinderbox!", 250, 0, 0);
}

if (%tutorial_progress > ^newbie_survival_instructor_open_skill_menu & inv_total(inv, net) < 1) {
    inv_add(inv, net, 1);
    ~objbox(net, "The Survival Guide gives you a |@blu@Net!", 250, 0, divide(^objbox_height, 2));
}

// Used later for OSRS so we'll leave it.
[proc,newbie_survival_instructor_replace_items_no_objboxes]
if (~tutorial_has_obj_on_person(bronze_axe) = false & inv_freespace(inv) > 0) {
    inv_add(inv, bronze_axe, 1);
}

if (inv_total(inv, tinderbox) < 1 & inv_freespace(inv) > 0) {
    inv_add(inv, tinderbox, 1);
}

if (inv_total(inv, net) < 1 & inv_freespace(inv) > 0) {
    inv_add(inv, net, 1);
}

[label,newbie_survival_instructor_fishing]
~chatnpc("<p,neutral>There's nothing like a good fire to warm the bones.|Next thing is getting food in our bellies. We'll need|something to cook. There are shrimp in the pond there.|So let's catch and cook some.");
if (npc_findexact(0_48_48_29_20, 0_48_48_newbiefishing) = true) {
    hint_npc(npc_uid);
}
inv_add(inv, net, 1);
%tutorial_progress = ^newbie_survival_instructor_fish_shrimps;
~tutorial_step_catch_shrimp;
~objbox(net, "The Survival Guide gives you a |@blu@Net!", 250, 0, divide(^objbox_height, 2));
~newbie_survival_instructor_replace_items;

// https://youtu.be/TSx2-jjPeIs?t=31
[label,newbie_survival_instructor_fishing_after_burning]
~newbie_survival_instructor_replace_items;
~chatnpc("<p,neutral>Hello again. Next thing is getting food in our bellies. We'll need something to cook. There are shrimp in the pond there. So let's catch and cook some.");

[label,newbie_survival_instructor_recap]
~chatnpc("<p,neutral>Hello again.");
~newbie_survival_instructor_replace_items;
~newbie_survival_instructor_recap_response(~p_choice5_header("Woodcutting.", 1, "Firemaking.", 2, "Fishing.", 3, "Cooking.", 4, "My stats.", 5, "What would you like to hear more about?"));

[proc,newbie_survival_instructor_recap_response](int $choice)
switch_int ($choice) {
    case 1 :
        ~survival_recap_woodcutting;
        ~survival_recap_questions($choice);
    case 2 :
        ~survival_recap_firemaking;
        ~survival_recap_questions($choice);
    case 3 :
        ~survival_recap_fishing;
        ~survival_recap_questions($choice);
    case 4 :
        ~survival_recap_cooking;
        ~survival_recap_questions($choice);
    case 5 :
        ~survival_recap_skills;
        ~survival_recap_questions($choice);
}

[proc,survival_recap_questions](int $choice)
switch_int($choice) {
    case 1 : ~newbie_survival_instructor_recap_response(~p_choice5_header("Firemaking.", 2, "Fishing.", 3, "Cooking.", 4, "My stats.", 5, "Nothing, thanks.", 6, "What would you like to hear more about?"));
    case 2 : ~newbie_survival_instructor_recap_response(~p_choice5_header("Woodcutting", 1, "Fishing.", 3, "Cooking.", 4, "My stats.", 5, "Nothing, thanks.", 6, "What would you like to hear more about?"));
    case 3 : ~newbie_survival_instructor_recap_response(~p_choice5_header("Woodcutting", 1, "Firemaking.", 2, "Cooking.", 4, "My stats.", 5, "Nothing, thanks.", 6, "What would you like to hear more about?"));
    case 4 : ~newbie_survival_instructor_recap_response(~p_choice5_header("Woodcutting.", 1, "Firemaking.", 2, "Cooking.", 4, "My stats.", 5, "Nothing, thanks.", 6, "What would you like to hear more about?"));
    case 5 : ~newbie_survival_instructor_recap_response(~p_choice5_header("Woodcutting.", 1, "Firemaking.", 2, "Fishing.", 3, "Cooking.", 4, "Nothing, thanks.", 6, "What would you like to hear more about?"));
}

[proc,survival_recap_skills]
~chatplayer("<p,neutral>Tell me about my stats again.");
~chatnpc("<p,neutral>To look at your stats, click on the bar graph icon found near your backpack icon. You can check your skill levels here.");
// OSRS specific
//~chatnpc("<p,neutral>Every skill is listed in the skills menu. Here you can see what your current levels are and how much experience you have.");
~chatnpc("<p,neutral>As you move your mouse over any of the icons here, look at the bottom of the menu and you will see the exact amount of experience you have and how much is needed to get to the next level.");
// Re-enable this line when we get skill menus.
//~chatnpc("<p,neutral>You can also click on a skill to open the relevant skillguide. In the skillguide, you can see all of the unlocks available in that skill. ");

[proc,survival_recap_woodcutting]
~chatplayer("<p,neutral>Tell me about Woodcutting again.");
~chatnpc("<p,neutral>Woodcutting, eh? Don't worry, newcomer, it's really very easy. Simply equip your axe and click on a nearby tree to chop away.");
~chatnpc("<p,neutral>As you explore the mainland you will discover many different kinds of trees that will require different levels of Woodcutting to chop down.");
~chatnpc("<p,neutral>Logs are not only useful for making fires. Many archers use the skill known as Fletching to craft their own bows and arrows from trees.");

[proc,survival_recap_fishing]
~chatplayer("<p,neutral>Tell me about Fishing again.");
~chatnpc("<p,neutral>Ah, yes. Fishing! Fishing is undoubtedly one of the more popular hobbies here in RuneScape!");
~chatnpc("<p,neutral>Whenever you see sparkling waters, you can be sure there's probably some good fishing to be had there!");
~chatnpc("<p,neutral>Not only are fish absolutely delicious when cooked, there are always fighters willing to buy a well cooked fish when they're low on health.");
~chatnpc("<p,quiz>I would recommend everybody has a go at Fishing at least once in their lives! Was there anything else you wished to hear again?");

[proc,survival_recap_firemaking]
~chatplayer("<p,neutral>Tell me about Firemaking again.");
~chatnpc("<p,neutral>Certainly, newcomer. When you have logs simply use your tinderbox on then. If successful, you will start a fire.");
~chatnpc("<p,neutral>You can also set fire to logs you find lying on the floor already, and some other things can also be set alight...");
~chatnpc("<p,neutral>A tinderbox is always a useful item to keep around!");

[proc,survival_recap_cooking]
~chatplayer("<p,neutral>Tell me about Cooking again.");
~chatnpc("<p,neutral>Yes, the most basic of survival techniques. Most simple meals can be cooked on a fire by right clicking on the food, selecting use, then left clicking on the fire.");
~chatnpc("<p,neutral>Eating food will restore a little health. The harder something is to cook, the more it will heal you. Somewhere around here is a chef who will tell you more about food and cooking it.");

[proc,set_hint_icon_newbie_survival_instructor]
if (npc_find(0_48_48_31_23, newbie_survival_instructor, 10, 0) = true) {
    hint_npc(npc_uid);
}



